include: macros/legion.def

page: main.cc, legion_frame
    module: cpp
    # run: cd out && make -f Makefile.gasnet -j8 && GASNET_PSHM_NODES=3 ./main -ll:cpu 2
    run: cd out && make -j8 && mpirun -n 3 ./main -ll:cpu 2

    $map register_task, TOP, INIT, DAXPY, CHECK
    $call @register_mapper

macros:
    X_type: double
    Y_type: double
    Z_type: double

fncode: TOP_task
    n_elements = 1024
    n_subregions = 4
    $call @get_n_subregions
    $print Running daxpy for $n_elements elements, partitioning into $n_subregions sub-regions...
    $call @create_logical_regions
    $call @launch_daxpy

    DUMP_STUB _delete_regions
    DUMP_STUB _delete_spaces

    subcode: create_logical_regions
        $call create_index_space, iA, n_elements

        $call create_field_space, fA, X, Y
        $call create_field_space, fB, Z

        $call create_logical_region, rA, iA, fA
        $call create_logical_region, rB, iA, fB

    subcode: launch_daxpy
        $call create_partition, iA, n_subregions, rA, rB
        &call launch_tasks_, -, INIT, iA_part
            $call region_require, rA_part, WRITE_DISCARD, X
        &call launch_tasks_, -, INIT, iA_part
            $call region_require, rA_part, WRITE_DISCARD, Y

        $my const double alpha = drand48()

        $(set:arg=alpha)
        &call launch_tasks_, -, DAXPY, iA_part
            $call region_require, rA_part, READ_ONLY, X, Y
            $call region_require, rB_part, WRITE_DISCARD, Z

        &call launch_tasks_, -, CHECK, iA_part
            $call region_require, rA_part, READ_ONLY, X, Y
            $call region_require, rB_part, READ_ONLY, Z

fncode: INIT_task
    &call task_
        &call with_fields, :X
            &call each_row
                $(F:X) = drand48()

fncode: DAXPY_task
    &call task_
        $call args, double alpha
        &call with_fields, X, Y, Z
            &call each_row
                $(F:Z) = alpha * $(F:X) + $(F:Y)

fncode: CHECK_task
    b_all_passed = true
    &call task_
        $call args, double alpha
        &call with_fields, X, Y, Z
            &call each_row
                f_expected = alpha * $(F:X) + $(F:Y)
                f_got = $(F:Z)
                $if f_expected != f_got
                    $print mismatch: $f_expected != $f_got
                    b_all_passed = false
    $if b_all_passed
        $print "All passed!"
    $else
        $print "Failed!"

#---------------------------------------- 
subcode: get_n_subregions
    $call get_partition_count, n_subregions

subcode: register_mapper
    $function fn_register_mapper(api mapper_register)
        $call set_mapper, AdversarialMapper, PartitioningMapper

    Runtime::add_registration_callback(fn_register_mapper);

#---------------------------------------- 
subcode: _autoload
    $include test_mapper.h, default_mapper.h
    NEWLINE
    using namespace Legion::Mapping;

    $call mapper_class, AdversarialMapper, TestMapper, select_task_options, slice_task, map_task, report_profiling
    $call mapper_class, PartitioningMapper, DefaultMapper, select_tunable_value

fncode: AdversarialMapper::(api mapper)
    $set_fn_attr(inherit) TestMapper(rt->get_mapper_runtime(), machine, proc)
    &call get_all_procs
        &call if_first_proc
            $call dump_all_procs
            &call get_all_mems
                $call dump_all_mems
            &call get_vis_mems
                $call dump_vis_mems

# immediately on task launch
fncode: AdversarialMapper::select_task_options(api mapper_options)
    options.inline_task = false # using parent's physical region
    options.stealable = false   # cilk-style semantics
    options.map_locally = true  # where is launched
    # options.profile_task      # whether to collect profiling

    $my Processor::Kind kind = select_random_processor_kind(ctx, task.task_id)
    options.initial_proc = select_random_processor(kind) # target proc

# IndexLauncher
fncode: AdversarialMapper::slice_task(api mapper_slice)
    output.slices.resize(input.domain.get_volume())

    $my unsigned idx = 0
    $switch input.domain.get_dim()
        $map random_slice, 1, 2, 3
        $of others
            $call die, "wrond dimension"

    subcode: random_slice(dim)
        $of $(dim)
            $: {
            $my Rect<$(dim)> rect = input.domain
            $for PointInRectIterator<$(dim)> it(rect); it(); it++, idx++
                $my Rect<$(dim)> slice(*it, *it) # single point index
                $(set:recurse=false)
                $(set:stealable=true)
                output.slices[idx] = TaskSlice(slice, select_random_processor(task.target_proc.kind()), $(recurse), $(stealable))
            $: }

# select memory in which to create physical instances of logical region
fncode: AdversarialMapper::map_task(api mapper_task)
    $call get_matching_variant
    output.target_procs.push_back(task.target_proc)
    $call get_premapped
    # ------------------
    &call get_layout_constraints
        &call each_task_region
            $(set:O=output.chosen_instances[i])
            $if premapped[i]
                continue
            $elif $(region).is_restricted()
                $(O) = input.valid_instances[i]
                continue
            $elif $(has_constraint:i)
                $call get_constraints, i
                map_constrained_requirement(ctx, $(region), TASK_MAPPING, constraints, $(O), task.target_proc)
            $else
                map_random_requirement(ctx, $(region), $(O), task.target_proc)

    output.task_priority = default_generate_random_integer()
    # profiling
    $(for:OperationStatus,OperationTimeline,RuntimeOverhead)
        output.task_prof_requests.add_measurement<ProfilingMeasurements::$1>();

    #---------------------------------------- 
    subcode: each_task_region
        $for i=0:(int)task.regions.size()
            $(set:region=task.regions[i])
            BLOCK

    #---------------------------------------- 
    subcode: get_matching_variant
        $my const std::map<VariantID,Processor::Kind> &variant_kinds = find_task_variants(ctx, task.task_id)
        $my std::vector<VariantID> variants
        &call map_iter variant_kinds
            $if task.target_proc.kind() == $(value)
                variants.push_back($(key))
        assert(!variants.empty())

        $if variants.size()>1
            $(set:i=default_generate_random_integer() % variants.size())
            $(set:i=0)
            output.chosen_variant = variants[$(i)]
        $else
            $(set:i=0)
            output.chosen_variant = variants[$(i)]

    subcode: get_premapped
        $: std::vector<bool> premapped(task.regions.size(), false);
        $for i=0:(int)input.premapped_regions.size()
            $my unsigned index = input.premapped_regions[i]
            output.chosen_instances[index] = input.valid_instances[index]
            premapped[index] = true

    subcode: get_layout_constraints
        $my const TaskLayoutConstraintSet &layout_constraints = runtime->find_task_layout_constraints(ctx, task.task_id, output.chosen_variant)
        $(set:L=layout_constraints.layouts)
        $(set:has_constraint=$(L).find($1) != $(L).end())
        BLOCK

        subcode: get_constraints(i)
            $my std::vector<LayoutConstraintID> constraints
            $for auto it=$(L).lower_bound($(i)); it!=$(L).upper_bound($(i)); it++
                constraints.push_back(it->second)

fncode: AdversarialMapper::report_profiling(api mapper_profiling)
    using namespace ProfilingMeasurements;

    &call get_measurement, OperationStatus, status
        $(for:COMPLETED_SUCCESSFULLY,COMPLETED_WITH_ERRORS,INTERRUPT_REQUESTED,TERMINATED_EARLY,CANCELLED)
            $case status->result == OperationStatus::$1
                $print "Task %s: $1", task.get_task_name()

    &call get_measurement, OperationTimeline, timeline
        $print "Operation timeline for task %s: ready=%lld, start=%lld, stop=%lld", task.get_task_name(), timeline->ready_time, timeline->start_time, timeline->end_time

    &call get_measurement, RuntimeOverhead, overhead
        $my long long total = overhead->application_time+overhead->runtime_time+overhead->wait_time
        $if total<=0
            total =1
        $print "Runtime overhead for task %s: runtime=%.1f%%, wait = %.1f%%", task.get_task_name(), 100.0 * overhead->runtime_time / total, 100.0*overhead->wait_time/total

    subcode: get_measurement(WHAT, var)
        $my $(WHAT) *$(var) = input.profiling_responses.get_measurement<$(WHAT)>()
        $if $(var)
            BLOCK
            delete $(var)
        $else
            $print "No $(WHAT) for task %s", task.get_task_name()

#---------------------------------------- 
fncode: PartitioningMapper::(api mapper)
    $set_fn_attr(inherit) DefaultMapper(rt->get_mapper_runtime(), machine, proc)

fncode: PartitioningMapper::select_tunable_value(api mapper_tunable)
    $if input.tunable_id == $(PART_TUNER_ID)
        Machine::ProcessorQuery all_procs(machine)
        all_procs.only_kind(Processor::LOC_PROC)
        runtime->pack_tunable<size_t>(all_procs.count(), output)
        return
    assert(false)

